programa ejemplo
{ [1-a] Realice un programa para que 2 robots junten todas las flores de los perímetros (15,15)-(30,35) y (40,40)-(55,60) respectivamente. Luego, deben posicionarse en la esquina (10,10) y depositar todas las flores juntadas.}

procesos
  proceso recogerFlores(ES cantF:numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      cantF := cantF + 1
  fin

  proceso recorrerLado(E lado:numero; ES cantF:numero)
  comenzar
    recogerFlores(cantF)
    repetir lado
      mover
      recogerFlores(cantF)
  fin

  proceso FloresEnDeposito(ES cantF:numero)
  variables
    miAv, miCa: numero
  comenzar
    BloquearEsquina(10,10)
    miAv := PosAv
    miCa := PosCa
    Pos(10,10)
    mientras (HayFlorEnLaBolsa)
      depositarFlor
      cantF := cantF - 1
    Pos(miAv, miCa)
    LiberarEsquina(10,10)
  fin

areas
  {Perímetro del robot 1: (15,15) -> (30,35)}
  a1: AreaP (15,15,15,34)
  a2: AreaP (15,35,29,35)
  a3: AreaP (30,16,30,35)
  a4: AreaP (16,15,30,15)

  {Perímetro del robot 2: (40,40) -> (55,60)}
  b1: AreaP (40,40,40,59)
  b2: AreaP (40,60,54,60)
  b3: AreaP (55,41,55,60)
  b4: AreaP (41,40,55,40)

  AreaDeposito: AreaC (10,10,10,10)
robots
  robot robot1
  variables
    alto, ancho, cantF: numero
  comenzar
    cantF := 0
    alto := 20
    ancho := 15
    repetir 2
      recorrerLado(alto, cantF)
      derecha
      recorrerLado(ancho, cantF)
      derecha
    FloresEnDeposito(cantF)
  fin

variables
  R1, R2: robot1

comenzar
  {Áreas del robot 1}
  AsignarArea(R1, a1)
  AsignarArea(R1, a2)
  AsignarArea(R1, a3)
  AsignarArea(R1, a4)
  AsignarArea(R1, AreaDeposito)

  {Áreas del robot 2}
  AsignarArea(R2, b1)
  AsignarArea(R2, b2)
  AsignarArea(R2, b3)
  AsignarArea(R2, b4)
  AsignarArea(R2, AreaDeposito)

  {Iniciar robots en los vértices de sus perímetros}
  Iniciar(R1, 15,15)
  Iniciar(R2, 40,40)
fin

